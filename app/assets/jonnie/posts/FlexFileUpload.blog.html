<a href="http://jonniespratley.com/wp-content/uploads/2008/10/flex_fileupload.png"> <img class="alignleft size-full wp-image-612" title="flex_fileupload" src="http://jonniespratley.com/wp-content/uploads/2008/10/flex_fileupload.png" alt="flex_fileupload" width="277" height="161" /></a> 

<p>Have you ever wanted to be able to upload a file in Flex? Or how about download a file in Flex? Well I am going to provide the code to do just that. </p>
<p>With Adobe Flex's sandbox issues I could only get this to work inside Air, but working with the web browser I kept on getting an IOError. I thought it could have been the write permissions on the folder but it wasn't.</p>
<p>I even put a crossdomain file and <code>Security.AllowAll("*")</code> but that didn't help at all. So if you want to send uploads with Air this will be perfect, other than that someone help me figure out this security issue.</p>
<p> Ok here is the Flex code for the three components you will use to build this application. </p>

Here is the item renderer <strong>ImageRenderer.mxml:</strong>
<pre class="brush: html">
	&lt;mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml"
		paddingLeft="2" 
		paddingBottom="2" 
		paddingRight="2" 
		paddingTop="2"
		horizontalScrollPolicy="off" 
		verticalScrollPolicy="off"
		horizontalAlign="center"
		verticalAlign="middle">	

		&lt;mx:Image id="img_thumb" 
			source="php/uploads/{ data.name }" 
			width="125" 
			height="100"/>
		&lt;mx:Label 
			text="Name: { data.name }"/>
		&lt;mx:Label 
			text="Size: { data.size }"/>
	</mx:VBox>
</pre>

Here is the download component <strong>FileDownload.mxml:</strong>
<pre class="brush: html">

	&lt;mx:VBox 
		xmlns:mx="http://www.adobe.com/2006/mxml" 
		width="100%" 
		height="100%" 
		creationComplete="init()" 
		xmlns:components="com.jonniespratley.advguidetoflex.view.chapter5.components.*">
		&lt;mx:Script>
			<![CDATA[
				import mx.rpc.http.HTTPService;
				import mx.rpc.events.FaultEvent;
				import mx.events.FlexEvent;	
				import mx.rpc.events.ResultEvent;
				import flash.events.*;
				import flash.net.*;		
				import mx.controls.Alert;
				import mx.events.*;
				import mx.managers.*;			

				/* Our File reference */
				private var fileRef:FileReference;

				/* Our image service that is goin to connect to get the xml */
				private var svcImages:HTTPService;

				/* Our XML url */
				private var filexmlURL:String = "php/flex_file_download.php";

				/* Create a new httpservice and file reference, then register some even listeners, then get the images */				
				private function init():void
				{

					svcImages = new HTTPService();
					fileRef = new FileReference();				
					svcImages.addEventListener( FaultEvent.FAULT, faultHandler );
					svcImages.addEventListener( ResultEvent.RESULT, resultHandler );
					getImages();
				}			

				/* Makes the call to the xml script */
				private function getImages():void 
				{
					svcImages.url = filexmlURL;
					svcImages.method = URLRequestMethod.GET;	
					svcImages.send(); 				
				}	

				/* Handles any results that happen */
				private function resultHandler( event:ResultEvent ):void 
				{
					lt_images.dataProvider = event.target.lastResult.files.file;
				}				

				/* Downloads the selected file to users computer */
				private function downloadFile( whatFile:String ):void
				{			
					var request:URLRequest = new URLRequest();
						request.method = URLRequestMethod.GET;					
						request.url = "http://advguidetoflex.jonniespratley.com/php/uploads/" + whatFile;					

						fileRef.download( request );			
				}

				/* Handles any faults that happen */			
				private function faultHandler( event:FaultEvent ):void 
				{
					Alert.show( "There was a error fetching the images.", "Error" );
				}
			]]>
		</mx:Script>

		<!--Download Button-->
		&lt;mx:Button label="Download"
			click="downloadFile( lt_images.selectedItem.name )"
			width="100%"/>

		<!--Displays our Images-->
		&lt;mx:TileList id="lt_images" width="100%" height="100%">
			&lt;mx:itemRenderer>
				&lt;mx:Component>
					<components:ImageRenderer/>
				</mx:Component>
			</mx:itemRenderer>		
		</mx:TileList>	

		<!--Refresh Button-->
		&lt;mx:Button label="Refresh"
			click="getImages()"
			width="100%"/>
	</mx:VBox>
</pre>


Here is our second component <strong>FileUpload.mxml:</strong>
<pre class="brush: html">

	&lt;mx:TitleWindow
		width="450"
		height="200"
		cornerRadius="10"
		title="Upload a File"
		showCloseButton="true"
		creationComplete="init()"
		close="PopUpManager.removePopUp( this )"
		xmlns:mx="http://www.adobe.com/2006/mxml">
		&lt;mx:Script>
			<![CDATA[
				import mx.managers.PopUpManager;

				import flash.net.*;
				import mx.controls.*;

				/* Location of our Upload script */
				private static const UPLOAD_URL:String = "php/flex_file_upload.php";
				private static const IMAGE:String = "image";
				private static const MODE_BROWSE:String = "fileModeBrowse";
				private static const MODE_UPLOAD:String = "fileModeUpload";

				[Bindable]public var fileMode:String;

				private var maxFileSize:Number = 3500;
				private var fileReference:FileReference;
				private var fileFilter:FileFilter;
				private var fileType:String;
				private var isReady:Boolean;

				/* Set up our application */
				private function init():void
				{

					PopUpManager.centerPopUp( this );

					this.isPopUp = false;

					//Set up the file upload components
					fileMode = "";
					pb_progress.label = "Select File Type";
					fileReference = new FileReference();
					addListeners();
				}

				/* Register all of our event handlers and listeners */
				private function addListeners():void
				{
					fileReference.addEventListener( Event.CANCEL, cancelHandler );
					fileReference.addEventListener( Event.SELECT, selectHandler );
					fileReference.addEventListener( IOErrorEvent.IO_ERROR, ioErrorHandler );
					fileReference.addEventListener( ProgressEvent.PROGRESS, progressHandler );
					fileReference.addEventListener( Event.COMPLETE, completeHandler );
				}

				/* We can assign the correct file filters based on the selected check box */
				private function assignFileType( event:Event ):void
				{
					//We switch depending on the id of the check box that is selected
					switch ( event.currentTarget.id )
					{
						//If the id is image
						case "cb_image":
							//Set varable to check if the cb_image box is selected
							var image_selected:Boolean = ( cb_image.selected ) ? true : false;
								if( image_selected != false )
								{
									//Enable the button
									isReady = true;

									//Set the fileType to image
									fileType = "image";

									//Display to user to browse
									pb_progress.label = "Browse for File";

									//Filter the files based on fileType
									filterFileType();
								} else {
									//Else disable the button
									isReady = false;
								}
						break;

						//If the id is a document
						case "cb_document":
							var document_selected:Boolean = ( cb_document.selected ) ? true : false;
								if ( document_selected != false )
								{
									//Enable the button
									isReady = true;

									//Set the fileType to document
									fileType = "document";

									//Display to user to browse
									pb_progress.label = "Browse for File";

									//Filter the files based on fileType
									filterFileType();
								} else {
									//Else disable the button
									isReady = false;
								}
						break;

						//If the id is a compressed
						case "cb_compressed":
							var compressed_selected:Boolean = ( cb_compressed.selected ) ? true : false;
								if ( compressed_selected != false )
								{
									//Enable the button
									isReady = true;

									//Set the fileType to document
									fileType = "compressed";

									//Display to user to browse
									pb_progress.label = "Browse for File";

									//Filter the files based on fileType
									filterFileType();
								} else {
									//Else disable the button
									isReady = false;
								}
						break;
					}
				}

				/* Filter the different filetypes */
				private function filterFileType():void
				{
					switch ( fileType )
					{
						case "image":
							fileFilter = new FileFilter( "Images (*.jpg; *.jpeg; *.gif; *.png;)",
							 										"*.jpg; *.jpeg; *.gif; *.png" );
						break;

						case "document":
							fileFilter = new FileFilter( "Documents (*.pdf; *.txt; *.doc; *.rtf;)",
																	"*.pdf; *.txt; *.doc; *.rtf;" );
						break;

						case "compressed":
							fileFilter = new FileFilter( "Compressed (*.zip; *.rar; *.7zip;)",
																	 "*.zip; *.rar; *.7zip;" );
						break;
					}

				}

				/* Submit the file to check if its legit */
				private function submit( _fileMode:String ):void
				{
					fileMode = _fileMode;

					if ( isReady != true )
					{
						Alert.show("Please select a file type!","ALERT", Alert.OK);
					} else if ( fileMode == MODE_BROWSE )
					{
						openBrowseWindow();
					} else if ( fileMode == MODE_UPLOAD )
					{
						uploadFile();
					}
				}

				/* Open up the browse for file window */
				private function openBrowseWindow():void
				{
				try
					{
						fileReference.browse([fileFilter]);
					}
					catch ( illegalOperation:IllegalOperationError )
					{
						Alert.show( String(illegalOperation.type), "illegal operation error", 0);
					}

				}

				/* Our select handler for checking the filesize, etc */
				private function selectHandler( event:Event ):void
				{
					//Get the filesize
					var fileSize:Number = Math.round( fileReference.size/1024 );

					//Set the filename input to the filename of the file
					txt_filename.text = fileReference.name;

					//Set the filesize text to the size of the file for upload
					txt_filesize.text = String( fileSize ) + "kb";

						//If the current file size is less or equal to our max file size then up
						if ( fileSize <= maxFileSize )
						{
							//Set the fileMode to upload
							fileMode = MODE_UPLOAD;

							//Set the label on the progress bar
							pb_progress.label = "Click Upload";

						} else {
							//If the file is to big, alert the user
							Alert.show( String("File is to large! \n\nPlease select a file smaller than "+ fileSize + "kb" ),
																								"File Size Error", Alert.OK );
						}
				}

				/* Sends the file to our script */
				private function uploadFile():void
				{
					var request:URLRequest = new URLRequest();
						request.url = UPLOAD_URL;

					var params:URLVariables = new URLVariables();
						params.FileType = fileType;
						request.method = URLRequestMethod.POST;


						request.data = params;

					fileReference.upload( request );
				}

				/* Handles our progress bar */
				private function progressHandler( event:ProgressEvent ):void
				{
					//Set the progress to our bytesLoaded, and bytesTotal
					pb_progress.setProgress( event.bytesLoaded, event.bytesTotal );

					//Set the label to uploading and do alittle math of displaying the current progress
					pb_progress.label = "Uploading " + Math.round( event.bytesLoaded / 1024 ) + " kb of " +
													   Math.round( event.bytesTotal / 1024 ) + " kb ";

				}

				/* Just incase our user wants to cancel the upload */
				private function cancelUpload():void
				{
					//Cancel the fileref
					fileReference.cancel();

					//Reset the uploader
					resetUploader();

					//Remove the popup
					PopUpManager.removePopUp( this );
				}

				/* We can handle the canceled download */
				private function cancelHandler( event:Event ):void
				{
					Alert.show( "File Upload Cancelled" );
				}

				/* IO Error handler */
				private function ioErrorHandler( event:IOErrorEvent ):void
				{
					//Alert of the error
					Alert.show( String( event.type ), "IOError", 0 );

					//Reset the uploader
					resetUploader();
				}

				/* Complete handler */
				private function completeHandler( event:Event ):void
				{
					var file:FileReference = FileReference( event.target );
					var fileURL:String = file.name;

					//Show the user the uploaded file
					Alert.show( String( fileURL ), "File Uploaded", Alert.OK );

					//Reset the uploader
					resetUploader();
				}

				/* Reset the filemode, inputs, progress bar and selected check boxes */
				private function resetUploader():void
				{
					fileMode = "";

					isReady = false;

					txt_filename.text = "";
					txt_filesize.text = "";

					pb_progress.label = " Select File Type";
					pb_progress.maximum = 0;
					pb_progress.minimum = 0;

				}
			]]>
		</mx:Script>
		&lt;mx:Form width="100%" height="100%">

			<!--File that is going to be uploaded-->
			&lt;mx:FormItem label="File name:" required="true" direction="horizontal" width="100%">
				&lt;mx:TextInput id="txt_filename"
					width="100%"/>
				&lt;mx:Button id="btn_browse"
					label="Browse"
					click="submit( MODE_BROWSE )"/>
			</mx:FormItem>

			<!--Size of the file to upload-->
			&lt;mx:Text id="txt_filesize" color="#000000" width="100%"/>

			<!--Type of file to upload-->
			&lt;mx:FormItem label="Type of Upload:" required="true" width="100%" direction="horizontal">
					&lt;mx:CheckBox id="cb_image"
						click="assignFileType( event )"
						label="Image"/>
					&lt;mx:CheckBox id="cb_document"
						click="assignFileType( event )"
						label="Document"/>
					&lt;mx:CheckBox id="cb_compressed"
						click="assignFileType( event )"
						label="Compressed"/>
			</mx:FormItem>
		</mx:Form>
		&lt;mx:ControlBar>
				<!--Cancel Button-->
				&lt;mx:Button id="btn_cancel"
					label="Cancel"
					click="cancelUpload()"/>

				<!--Progress Bar-->
				&lt;mx:ProgressBar id="pb_progress"
					labelPlacement="center"
					trackHeight="8"
					width="100%"/>

				<!--Upload Button-->
				&lt;mx:Button label="Upload"
					id="upload_btn"
					click="submit( MODE_UPLOAD )"
					enabled="{ fileMode == MODE_UPLOAD }"/>
		</mx:ControlBar>
	</mx:TitleWindow>
</pre>

And our main view <strong>Chapter5.mxml:</strong> ( From my guide )
<pre class="brush: html">
	&lt;mx:VBox 
		width="100%" 
		height="100%" 
		xmlns:mx="http://www.adobe.com/2006/mxml" 
		xmlns:components="com.jonniespratley.advguidetoflex.view.chapter5.components.*">
		&lt;mx:Script>
			<![CDATA[
				import com.jonniespratley.advguidetoflex.view.chapter5.components.FileUpload;
				import mx.managers.PopUpManager;
				import mx.core.IFlexDisplayObject;

				private var uploadBox:IFlexDisplayObject;

				private function openUploadBox():void
				{
					PopUpManager.createPopUp( this, FileUpload, true );
				}
			]]>
		</mx:Script>
		&lt;mx:Button 
			label="Upload a File" 
			width="100%"
			click="openUploadBox()"/>

		<components:FileDownload id="fileDownload"/>

	</mx:VBox>
</pre>

For the back-end we are using PHP, so we have two scripts here one is for processing the upload and then outputting XML with the results, and one for getting the data from the database for the display in XML.

<strong>flex_file_upload.php:</strong>
<pre class="brush: php">

	/** *******************************************************************
	 * Working File Upload Server Script for Flex
	 *
	 * MySnippets
	 * Free for use
	 *
	 * @author  Jonnie Spratley
	 * @contact jonniespratley@gmail.com
	 ******************************************************************* */
	if (isset( $_POST['FileType']) && isset($_FILES['Filedata']) ) 
	{

	/* Establish a connection to database */
	$link = mysql_connect('localhost', 'spratley_guest', 'guest') or die('Could not connect: ' . mysql_error());

	/* Select the database */
	mysql_select_db( 'spratley_tutorials', $link ) or die ( mysql_error() );

	/* Set the upload directory */
	$upload_dir = "uploads/";

	/* PHP temp_name variable for file upload */
	$temp_name = $_FILES['Filedata']['tmp_name'];

	/* PHP file_name variable sent from Flex */
	$file_name = $_FILES['Filedata']['name'];

	/* PHP file_size variable sent from Flex */
	$file_size = $_FILES['Filedata']['size'];

	/* PHP file_type variable sent from Flex */
	$file_type = $_FILES['Filedata']['type'];

	/* PHP file_ext variable set from Flex */
	$file_ext = $_FILES['Filedata']['extension'];

	/* Set the file_url to the hosturl and the updload directory and filename */
	$file_url = $_SERVER['HTTP_HOST'] . $upload_dir . $file_name;

	/* Replace any computer garbage  */
	$file_name = str_replace("\\","",$file_name);

	/* Replace any garbage */
	$file_name = str_replace("'","",$file_name);

	/* Set up the filepath */
	$file_path = $upload_dir.$file_name;


	/* Insert info into fle_uploads table in your mysql database */
	$insert = "INSERT INTO flex_uploads ( file_name, file_size, file_type, file_ext, file_url ) 
			   VALUES ( '$file_name', 
						'$file_size', 
						'$file_type', 
						'$file_ext',
						'$file_url' )";

	/* Execute the query */				  
	$query = mysql_query($insert) or die(mysql_error());

	/* Get the last insert id */
	$lastid = mysql_insert_id();

	/* Move the uploaded file */
	$result  =  move_uploaded_file( $temp_name, $file_path );

	/* Set the content type for the browser */
	header("Content-type: text/xml");

	/* Set the header xml version */
	$xml_output = "<?xml version=\"1.0\"?>\n";

	/* Set the root node for the xml */
	$xml_output .= "<results>\n";

	/* If file was moved and inserted */
	if ( $result ) 
	{
		/* Build the XML if file was successful */			     
		/* Set the child node */
	    $xml_output .= "\t<result>\n";

	    /* status */
	    $xml_output .= "\t\t<status>" . "Successful" . "</status>\n";

	    /* file_name variable */
	    $xml_output .= "\t\t<name>" . $file_name . "</name>\n";

	    /* file_size variable */
		$xml_output .= "\t\t<size>" . $file_size . "</size>\n";

		/* file_type variable */
		$xml_output .= "\t\t<type>" . $file_type . "</type>\n";

		/* file_url variable */
		$xml_output .= "\t\t<url>" . $file_url . "</url>\n";

	    /* Close the child result now and print the child result node out */
	    $xml_output .= "\t</result>\n";

	}

	} else {

		/* Set the content type for the browser */
		header("Content-type: text/xml");

		/* Set the header xml version */
		$xml_output = "<?xml version=\"1.0\"?>\n";

		/* Set the root node for the xml */
		$xml_output .= "<results>\n";

		/* Build the XML if file was un-successful */			     
		/* Set the child node */
	    $xml_output .= "\t<result>\n";

	    /* status */
	    $xml_output .= "\t\t<status>" . "Error" . "</status>\n";

	    /* file_name variable */
	    $xml_output .= "\t\t<name>" . $file_name . "</name>\n";

	    /* file_name variable */
	    $xml_output .= "\t\t<message>" . "There was a problem uploading your file." . "</message>\n";


	    /* Close the child result now and print the child result node out */
	    $xml_output .= "\t</result>\n";

	}

	/* Close the parent results node */
	$xml_output .= "</results>";

	/* Output all of the xml */
	echo $xml_output; 

</pre>



<strong>flex_file_download.php:</strong>
<pre class="brush: php">
	/** ****************************************************************
	 * Working File Download XML Server Script for Flex
	 *
	 * MySnippets
	 * Free for use
	 *
	 * @author  Jonnie Spratley
	 * @contact jonniespratley@gmail.com
	 ******************************************************************* */
	/* Set the content type for the browser */
	header("Content-type: text/xml");

	/* Establish a connection to database */
	$link = mysql_connect('localhost', 'spratley_guest', 'guest') or die('Could not connect: ' . mysql_error());

	/* Select the database */
	mysql_select_db( 'spratley_tutorials', $link ) or die ( mysql_error() );

	/* Build the query for the table */
	$query = "SELECT * FROM flex_uploads ORDER BY file_date DESC";

	/* Execute the query on the table */
	$resultID = mysql_query( $query, $link ) or die( "Data not found." );

	/* Set the header xml version */
	$xml_output = "<?xml version=\"1.0\"?>\n";

	/* Set the root node for the xml */
	$xml_output .= "<files>\n";

	/* Loop through all records in the query and output */
	for( $x = 0 ; $x < mysql_num_rows( $resultID ) ; $x++ )
	{
		/* Result rows */
	    $row = mysql_fetch_assoc( $resultID );

		/* Set the child node */
	    $xml_output .= "\t<file>\n";

		/* Set every node inside the child file node (id) */
	    $xml_output .= "\t\t<id>" . $row['file_id'] . "</id>\n";

	    /* file_name table column */
	    $xml_output .= "\t\t<name>" . $row['file_name'] . "</name>\n";

	    /* file_size table column */
		$xml_output .= "\t\t<size>" . $row['file_size'] . "</size>\n";

		/* file_type table column */
		$xml_output .= "\t\t<type>" . $row['file_type'] . "</type>\n";

		/* file_url table column */
		$xml_output .= "\t\t<url>" . $row['file_url'] . "</url>\n";

		/* file_date table column */
		$xml_output .= "\t\t<date>" . $row['file_date'] . "</date>\n";

	    /* Close the child file now and print the child file node out */
	    $xml_output .= "\t</file>\n";

	}/* Ends the loop */

	/* Close the parent files node */
	$xml_output .= "</files>";

	/* Output all of the xml */
	echo $xml_output; 

</pre>

I hope that this was some great help for people building application where uploading it a must.